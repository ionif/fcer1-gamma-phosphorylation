# Filename: example1_starting_point.bngl
#   This plain-text file is a BioNetGen input file.
#   It is derived from File S1 of Kozer et al. (2013).
#   Edits of File S1 were made by Brandon R. Thomas and William S. Hlavacek.
# Date of last modification: 17 June 2015
# A modified version of this file (suitable for processing by GenFit) is referred to 
# as Supplementary File S1 by Thomas et al.

begin model

# A model for activation of the epidermal growth factor receptor (EGFR)
#   This model is the model of Kozer et al. (2013).

# References:
# Elleman TC et al. (2001) Biochemistry 40: 8930â€“8939.
# Faeder JR et al. (2009) Methods Mol Biol 500: 113-167.
# Kleiman et al. (2011) Mol Cell 43: 723-737.
# Kozer N et al. (2013) Mol BioSyst 9: 1849-1863.
# Low-Nam ST et al. (2011) Nat Struct Mol Biol 18: 1244-1249.
# Macdonald JL, Pike LJ (2008) Proc Natl Acad Sci USA 105: 112-117.
# Thomas BR et al. "GenFit: a fitting tool compatible with BioNetGen, NFsim, and distributed computing environments"

begin parameters

#See Table 1 in Kozer et al. (2013) for additional information about parameter values.
#It should be noted that BioNetGen treats rate constants as single-site rate constants. Thus, care must be taken to specify such rate constants, i.e., rate constants that do not incorporate statistical factors (which arise, for example, because a reactant has multiple indistinguishable copies of a reactive site) or factors that arise because of reactions that have the form A+A->product(s). See Faeder et al. (2009) for more details.

LT 30                   	#  Total ligand concentration (nM). A default concentration of 30 nM was used in cases where the ligand dose was not varied. In dose-response analyses, this concentration is varied in the range 0.001-100 nM.  
RT 0.09                 	#  Total receptor concentration (nM). The value is based on 90,000 EGFR per cell and a density of 6*10^6 cell/mL.  

K11     4.6                  #  Equilibrium association constant (/nM). The constant characterizes binding of EGF with EGFR, which is not crosslinked with other EGFR via the ectodomain.The value is taken from Macdonald and Pike (2008), where it represents EGF binding with monomeric EGFR.   
k11r	0.02                 #  Dissociation constant (/s). This value is taken from Elleman et al. (2001). 
k11f	0.09                 #  Association constant (/nM/s). The value is derived from k11f = K11*k11r. 

K21     5.3                  #  Equilibrium association constant (/nM). The constant characterizes binding of EGF with EGFR that is crosslinked via ectodomain with a second EGFR, and the crosslinked receptors both are free of EGF.The value is taken from Macdonald and Pike (2008). 
k21r	0.02                 #  Single-site dissociation constaint (/s). We assume k21r to be the same as k11r. 
k21f	0.053                #  Association constant (/nM/s). The value is derived from k21f = K21*k21r/2. Division by 2 is a correction to obtain the single-site association rate constant (k21f) from the effective equilibrium association constant (K21).     

K22     0.34                 #  Equilibrium association constant (/nM). The constant characterizes binding of EGF with EGFR that is crosslinked via ectodomain with a second EGFR and the second EGFR is occupied by EGF.The value is taken from Macdonald and Pike (2008). 
k22r	0.2                  #  Single-site dissociation constaint (/s). We assume that k22r is 10-fold larger than k11r. This assumption is consistent with the difference between K11 (or K21) and K22. 
k22f	0.136                #  Single-site association constant in (/nM/s). The value is erived from k22f = 2*K22*k22r. Multiplication with 2 is applied to get single site association rate constant (k22f) from effective equilibrium association constant (K22).  

L20     212                  #  Equlibrium crossliinking constaint (/nM). The constant characterizes ectodomain-ectodomain interaction when both receptors are free from EGF. This parameter value is based on the value of 530 dm^2/nmol reported by Macdonald and Pike (2008) and the following assumptions: a receptor surface density of 1.175e-3 nmol/dm^2 (~20 eGFP-EGFR on average for a beam with a radius of 0.3 microns) for a cell line expressing 30,000 copies of EGFR per cell, receptor density is proportional to total receptor copy number, and a total receptor concentration of approximately 0.009 nM in the experiments of Macdonald and Pike (2008) involving cells expressing 87,000 copies of EGFR per cell (from the x-intercept of the Scatchard plot shown in the inset of Fig. 5A in the paper of Macdonald and Pike). In other words, we assume that the dimensionless quantity L20*RT is approximately 1.8 for cells with 87,000 copies of EGFR per cell. Moreover, we assume that the total number of cells used in the experiments of Macdonald and Pike (for a cell line with 87,000 copies of EGFR per cell) was such that the total concentration of EGFR was approximately 0.009 nM. If we further assume that the total number of cells used was the same in all experiments of Macdonald and Pike, then the dimensional value of L20 (in units consistent with those used for total EGF concentration) is 212 /nM (obtained by dividing the dimesionless quantity L20*RT by RT when RT is expressed in the same units as those used for total EGF concentration).
l20r	1.24                 #  Dissociation rate constant in (/s). The value is taken from Low-Nam et al. (2011).
l20f	526                  #  Association rate constant in (/nM/s). Derived from l20f = 2*L20*l20r. Multiplication factor 2 is applied because the reactant pair are the same species (Fig.1). 

L21     244                  #  Equilibrium crosslinking constant (/nM). The constant characterizes ectodomain-ectodomain interaction when only one receptor is bound to EGF. The value is derived from the detailed balance relation, L21 = K21*L20/K11. 
l21r	0.738                #  Dissociation rate constant in (/s). The value is taken from Low-Nam et al. (2011).
l21f    180                  #  Association rate constant (/nM/s). The value is derived from l21f = L21*l21r.

L22     18.0                 #  Equilibrium crosslinking constant (/nM). The constant characterizes ectodomain-ectodomain interaction when both receptors are bound to EGF. The value is derived from the detailed balance relation, L22 = K22*L21/K11. 
l22r	0.272                #  Dissociation rate constant in (/s). The value is taken from Low-Nam et al. (2011).
l22f    9.79                 #  Association rate constant (/nM/s). The value is derived from l22f = 2*L22*l22r. Multiplication factor 2 is applied because the reactant pair are the same species (Fig.1).

k_o     6.0                  #  Rate constant for ligand-mediated activating conformation change in the cytosolic tail of EGFR (/s). The value is obtained from fitting.
k_c     1.6                  #  Rate constant for ligand-independent deactivating conformation change in the cytosolic tail of EGFR (/s). The value is obtained from fitting.    

kaf     15.4                 #  Rate constant for EGFR tail-tail association mediated by EGFR kinase domain and juxtamembrane domains (/s). The value is obtained from fitting. 
kar     8.89                 #  Rate constant for EGFR tail-tail dissociation (/s). The value is obtained from model fitting.  

kp      1                    #  EGFR phosphorylation rate constant (/s). The value is consistent with data in Kleiman et al. (2011).  
kdp     1                    #  EGFR dephoshporylation rate constant (/s). The value is consistent with data in Kleiman et al. (2011). 

chi_r   4.37e+04             #  Enhancement factor for ring-closure reactions (nM). The value is obtained from fitting.  

# scaling factors for unprocessed datasets 1-4
#   Each scaling factor can be used to relate a model output to a measurement.
#   dataset 1 of Fig. 2B (dose-response data; EGFR cluster density vs. EGF dose)
#   alpha1*Clusters=measurement value
alpha1 972 # (clusters per square micron)/nM
#   dataset 2 of Fig. 3B (time-series data; EGFR cluster density vs. time, 30 nM EGF)
#   alpha2*Clusters=measurement value
alpha2 820 # (clusters per square micron)/nM
#   dataset 3 of Fig. 2D (dose-response data; EGFR phosphorylation vs. EGF dose)
#   alpha3*pEGFR=measurement value
alpha3 23.4 # (a.u.)/nM
#   dataset 4 of Fig. 3D (time-series data; EGFR phosphorylation vs. time, 30 nM EGF)
#   alpha4*pEGFR=measurement value
alpha4 21.0 # (a.u.)/nM

# average of all measurements for dataset 1 (Fig. 2B)
avg1 34.5 # (clusters per square micron)
# average of all measurements for dataset 2 (Fig. 3B)
avg2 25.6 # (clusters per square micron)
# average of all measurements for dataset 3 (Fig. 2D)
avg3 0.578 # (arbitrary units, relative fluorescence intensity) 
# average of all measurements for dataset 4 (Fig. 3D)
avg4 0.75 # (arbitrary units, relative fluorescence intensity)

# scaling factors for preprocessed datasets
#   These scaling factors are used to relate model outputs to measurements
#   when each dataset has been scaled so that the average of all measurements is 1.
alpha1_pre=alpha1/avg1 # /nM
alpha2_pre=alpha2/avg2 # /nM
alpha3_pre=alpha3/avg3 # /nM
alpha4_pre=alpha4/avg4 # /nM

# KD for ligand-independent receptor dimerization
KD = 2*l20r/l20f
# Find MT (monomer abundance) and DT (dimer abundance), 
#   which define the monomer-dimer equilibrium in the absence of ligand.
term = -1 + sqrt(1+8*RT/KD)
MT = term*KD/4
DT = term*term*KD/16

end parameters

begin molecule types
EGF(rec)
EGFR(back,lig,cd~c~o,Y~u~p)
end molecule types

begin seed species
EGF(rec) LT 
EGFR(back,lig,cd~c,Y~u) MT
EGFR(back!1,lig,cd~c,Y~u).EGFR(back!1,lig,cd~c,Y~u) DT
end seed species

begin observables

# Unbound EGF
Molecules EGFfree EGF(rec)

# Ligand-free EGFR
Molecules EGFRfree EGFR(lig)

# Clusters 
Species Clusters EGFR==1 EGFR==2 EGFR==3 EGFR==4

# EGFR monomer
Species monomer EGFR==1

# EGFR dimer
Species dimer EGFR==2

# EGFR trimer
Species trimer EGFR==3

# EGFR tetramer
Species tetramer EGFR>3 # The pattern 'EGFR>3' can be used because the model does not permit oligomers larger than size 4.

# Phosphorylated-EGFR
Molecules pEGFR  EGFR(Y~p)

end observables

begin functions

# The functions below generate outputs that can be directly compared to
#   preprocessed data of Kozer et al. (2013).
pre1_dose()=alpha1_pre*Clusters # .scan file output (Fig. 2B)
pre2_time()=alpha2_pre*Clusters # .gdat file output (Fig. 3B)
pre3_dose()=alpha3_pre*pEGFR # .scan file output (Fig. 2D)
pre4_time()=alpha4_pre*pEGFR # .gdat file output (Fig. 3D)

# The functions below generate outputs that can be directly compared to
#   the (unprocessed) data reported by Kozer et al. (2013).
out1_cDens_dose()=alpha1*Clusters # .scan file output (Fig. 2B)
out2_cDens_time()=alpha2*Clusters # .gdat file output (Fig. 3B)
out3_pEGFR_dose()=alpha3*pEGFR # .scan file output (Fig. 2D)
out4_pEGFR_time()=alpha4*pEGFR # .gdat file output (Fig. 3D)

end functions

begin reaction rules

# EGF binding to EGFR not crosslinked via the ectodomain. 
EGF(rec) + EGFR(back,lig) <-> EGF(rec!1).EGFR(back,lig!1) k11f,k11r

# EGF binding to EGFR that is crosslinked via the ectodomain to a second EGFR and both crosslinked EGFR are free of EGF. 
EGF(rec) + EGFR(back!1,lig).EGFR(back!1,lig) <-> EGF(rec!2).EGFR(back!1,lig!2).EGFR(back!1,lig) k21f,k21r

# EGF binding to EGFR that is crosslinked via the ectodomain to a second EGFR and the second EGFR is bound to EGF.  
EGF(rec) + EGF(rec!1).EGFR(back!2,lig!1).EGFR(back!2,lig) <-> EGF(rec!3).EGF(rec!1).EGFR(back!2,lig!1).EGFR(back!2,lig!3) k22f,k22r

# Ectodomain-ectodomain crosslinking of two EGFR that are not tethered and both EGFR are free of EGF.
EGFR(back,lig) + EGFR(back,lig) <-> EGFR(back!1,lig).EGFR(back!1,lig) l20f,l20r 

# Ectodomain-ectodomain crosslinking of two EGFR that are not tethered and only one EGFR is free of EGF. 
EGFR(back,lig) + EGF(rec!1).EGFR(back,lig!1) <-> EGFR(back!2,lig).EGF(rec!1).EGFR(back!2,lig!1) l21f,l21r

# Ectodomain-ectodomain crosslinking of two EGFR that are not tethered and both EGFR are occupied by EGF. 
EGF(rec!1).EGFR(back,lig!1) + EGF(rec!2).EGFR(back,lig!2) <-> EGF(rec!1).EGFR(back!3,lig!1).EGF(rec!2).EGFR(back!3,lig!2) l22f,l22r

# Ectodomain-ectodomain crosslinking of two EGFR that are tethered and both EGFR are free of EGF (ring-closure reaction)
EGFR(cd~o!4,back,lig).EGFR(cd~o!2,back,lig).EGFR(cd~o!2,back!3).EGFR(cd~o!4,back!3) <-> EGFR(cd~o!4,back!1,lig).EGFR(cd~o!2,back!1,lig).EGFR(cd~o!2,back!3).EGFR(cd~o!4,back!3) chi_r*l20f, l20r

# Ectodomain-ectodomain crosslinking of two EGFR that are tethered and only one EGFR is free of EGF (ring-closure reaction)
EGFR(cd~o!4,back,lig).EGFR(cd~o!2,back,lig!+).EGFR(cd~o!2,back!3).EGFR(cd~o!4,back!3) <-> EGFR(cd~o!4,back!1,lig).EGFR(cd~o!2,back!1,lig!+).EGFR(cd~o!2,back!3).EGFR(cd~o!4,back!3) chi_r*l21f, l21r

# Ectodomain-ectodomain crosslinking of two EGFR that are tethered and both EGFR are occuplied by EGF (ring-closure reaction)
EGFR(cd~o!4,back,lig!+).EGFR(cd~o!2,back,lig!+).EGFR(cd~o!2,back!3).EGFR(cd~o!4,back!3) <-> EGFR(cd~o!4,back!1,lig!+).EGFR(cd~o!2,back!1,lig!+).EGFR(cd~o!2,back!3).EGFR(cd~o!4,back!3) chi_r*l22f, l22r

# EGF-induced activating conformation change of EGFR cytosolic tail
EGFR(lig!+,cd~c) -> EGFR(lig!+,cd~o)  k_o

# EGF-independent deactivating conformatoin change of EGFR cytosolic tail
EGFR(cd~o) -> EGFR(cd~c) k_c

# Crosslinking of conformationally-modified cytosolic tails of EGFR that are not tethered
EGFR(cd~o) + EGFR(cd~o) <-> EGFR(cd~o!1).EGFR(cd~o!1) kaf, kar

# Crosslinking of conformationally-modified cytosolic tails of EGFR that are tethered (ring-closure reaction)
EGFR(cd~o,back!1).EGFR(cd~o!3,back!1).EGFR(cd~o!3,back!2).EGFR(cd~o,back!2) <-> EGFR(cd~o!4,back!1).EGFR(cd~o!3,back!1).EGFR(cd~o!3,back!2).EGFR(cd~o!4,back!2) chi_r*kaf, kar

# EGFR phosphorylation
EGFR(cd~o!1).EGFR(cd~o!1,Y~u) -> EGFR(cd~o!1).EGFR(cd~o!1,Y~p) kp 

# EGFR dephosphorylation
EGFR(Y~p) -> EGFR(Y~u)   kdp

end reaction rules

end model

begin actions

# find the reaction network implied by rules of the model
#   The max_stoich setting below prohibits oligomers larger than tetramers.
generate_network({overwrite=>1,max_stoich=>{EGF=>4,EGFR=>4}})

# The generate_network command can be bypassed if a .net file is available
#   (i.e., if a reaction network has already been derived from the rules of the model
#   by calling generate_network).
#   To bypass the generate_network command, comment it out (by placing a '#' character
#   at the start of the line above). Then... create a new .bngl file (name.bngl) that 
#   starts with a line of the following form:
#readFile({file=>"path/example1.net"})
#   (Do not include the '#' character.)
#   Then add simulation commands (such as those below).
#   From the command line, type BNG2.pl name.bngl to run without network generation.
#   If you need to change parameter values, you can edit parameter values in the .net file
#   using a text editor. The .net file is a plain-text file.

saveConcentrations() # save initial conditions specified in the seed species block
saveParameters() # save parameter values specified in the parameters block
# perform a parameter scan; outputs will be sent to .scan file
parameter_scan({parameter=>"LT",par_min=>0.001,par_max=>100,n_scan_pts=>6,log_scale=>1,\
                method=>"ode",t_start=>0,t_end=>1200,n_steps=>20,get_final_state=>0,\
                print_functions=>1})

resetConcentrations() # reset to initial conditions specified in seed species block
resetParameters() # reset to parameter values specified in the parameters block
# perform a forward simulation; outputs will be sent to .gdat file
#   report results at 30 s intervals
simulate({method=>"ode",t_start=>0,t_end=>600,n_steps=>20,\
          print_functions=>1})

end actions
